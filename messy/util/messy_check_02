#! /bin/tcsh -f

### CHECK f90 FILES FOR MESSy-CONFORMITY:
### 02: SMCL-files MUST NOT USE OTHER SUBMODELS
###
### Author: Patrick Jöckel, MPICH, Mainz, Germany, Mar 2004

if ($# != 2) then
   echo "USAGE: `basename $0` <BASEMODEL> <f90-file>"
   exit 1
endif

set BASEMODEL = $1
set file = $2
set checkid = 02

### BASEMODEL SETTINGS
if ( -r messy/$BASEMODEL/messy_setup != 0 ) then
   source messy/$BASEMODEL/messy_setup
else
   echo "ERROR: MESSY SETUP messy/$BASEMODEL/messy_setup IS UNREADABLE\!"
   exit 1
endif

# SET SUBMODEL
set smodel = `echo $file | awk -F '_' '{print tolower($2)}' | sed 's|\.f90||g'`

# LIST OF MODULES IN USE STATEMENTS
set q_use = `grep -iE "^ *USE " $file  | sed 's|\,| |g' | awk '{print tolower($2)}'`
# NUMBER OF USE STATEMENTS
set n = ${#q_use}

#######################################
# LIST OF USED messy-MODULES
set messy = ()
@ count = 1
while($count <= $n)
# echo $q_use[$count]
  # CHECK FOR MESSy: "messy_"
  set sf = `echo $q_use[$count] | awk '{sf=substr($0,1,6); if (sf=="messy_") print}' `
  set messy = ($sf $messy)
@ count++
end
set m = ${#messy}
#######################################

#######################################
set wrong = ()
@ count = 1
while($count <= $m)
  unset sf
  set sf = `echo $messy[$count] | sed 's|_| |g'`

  # allow messy_cmn_*_mem files
  #if ($messy[$count] =~ messy_cmn_*_mem) then
  if ($messy[$count] =~ messy_cmn_*) then
     @ count++
     continue
  endif

  # allow messy_attila_tools[_e5] in lg* submodels
  if (($messy[$count] =~ messy_attila_tools*) && ($smodel =~ lg*)) then
     @ count++
     continue
  endif

  switch($sf[2]) # $sf[1] == messy
  case "$smodel":
    breaksw
  case "ncregrid":
    breaksw
  case "scav":
    if ($file != 'messy/smcl/messy_convect_tiedtke.f90') then
       set wrong = ($messy[$count] $wrong)
    endif
    breaksw
  case "main":
    if ($sf[3] == "switch") then
       set wrong = ($messy[$count] $wrong)
    endif
    breaksw
  case "attila":
    if (${#sf} != 4) then  # != messy_attila_tools_e5
       set wrong = ($messy[$count] $wrong)
    endif
    breaksw
  default:
    set wrong = ($wrong $messy[$count])
    breaksw
  endsw

@ count++
end
#######################################

#######################################
switch ($smodel)
   ### EXCLUDE INFRASTRUCTURE
   case "ncregrid":
   case "main":
	breaksw
   default:
        if (${#wrong} > 0) then
            echo '-----------------------------------------------------'
            echo 'SUBMODEL: '$smodel
	    echo 'CHECK-ID: '$checkid
            echo 'FILE    : '$file
            echo ${#wrong}' NON-MESSy-CONFORM USE-STATEMENT(S) FOUND: '
            echo ${wrong} | tr ' ' '\n'
            echo '-----------------------------------------------------'
        endif
        breaksw
endsw
#######################################

exit 0
