\cancel mode verify
cancel data/all
cancel variable/all
cancel region/all
!cancel viewport

! ------------------------------------------------------------------
! THIS FERRET-JNL CONVERTS A S4D FILE (ASCII) INTO netCDF
! NOTE: pressure is converted from hPa to Pa to be consistent with
!       s4d_sample.jnl
! ------------------------------------------------------------------
! EXAMPLE:
! go s4d_pos2nc C1F_F_20000118.pos
! ------------------------------------------------------------------

! ------------------------------------------------------------------
! USER INTERFACE
! ------------------------------------------------------------------
! S4D position information
define symbol s4dfile = $1

! ------------------------------------------------------------------
! SEPERATE PATH AND FILNAME
! ------------------------------------------------------------------
let s4dbase = {spawn:"basename ($s4dfile)"}
define symbol s4dbase = `s4dbase[i=1]`
let s4dpath = {spawn:"dirname ($s4dfile)"}
define symbol s4dpath = `s4dpath[i=1]`
cancel variable s4dbase
cancel variable s4dpath

! ------------------------------------------------------------------
let mostr = {'Jan','Feb','Mar','Apr','May','Jun',\
             'Jul','Aug','Sep','Oct','Nov','Dec'}

! read transect information from ASCII-file (9 columns)
! time UTC !
! year month day hour minute second latitude longitude pressure [hPa]
   ! NOTE: for long files an index-grid must be defined ...
   let nlines = {spawn: "wc -l ($s4dfile)"}
   let tdim = `STRFLOAT(nlines[i=1])`
   define axis/x=1:`tdim`:1 xtdim
   define grid/x=xtdim g1d
   ! year month day hour minute second latitude longitude pressure [hPa]
   file/grid=g1d/var="qyy, qmo, qdy, qhr, qmi, qse, qlon, qlat, qpress" \
       "($s4dpath)/($s4dbase)"
   ! define time axis for transect
   define symbol l1 = `qmo[i=1]`
   define symbol mstr = `mostr[i=($l1)]`
   define symbol tzero = `qdy[i=1],zw=2`-($mstr)-`qyy[i=1],zw=4`
   define axis/from/t0="($tzero)"/name=tax/unit=hours \
          (DAYS1900(qyy,qmo,qdy) - DAYS1900(qyy[i=1],qmo[i=1],qdy[i=1]) )*24 +\
           qhr +qmi/60 + qse/3600
   ! ... for same strange reason, this definition must occir twice to make
   ! it work correctly - weird!
   define axis/from/t0="($tzero)"/name=tax/unit=hours \
          (DAYS1900(qyy,qmo,qdy) - DAYS1900(qyy[i=1],qmo[i=1],qdy[i=1]) )*24 +\
           qhr +qmi/60 + qse/3600
   ! put lat/lon/press information onto time axis
   define variable/title="longitude"/unit=degrees_east \
          lon = RESHAPE(qlon, T[gt=tax])
   define variable/title="latitude"/unit=degrees_north \
          lat = RESHAPE(qlat, T[gt=tax])
   ! NOTE: pressure in S4D files is in hPa !!!
   define variable/title="pressure"/unit=Pa \
          press = RESHAPE(qpress, T[gt=tax])*100
   ! put time onto time axis
   define variable/title="year"/unit="UTC"   year   = RESHAPE(qyy, T[gt=tax])
   define variable/title="month"/unit="UTC"  month  = RESHAPE(qmo, T[gt=tax])
   define variable/title="day"/unit="UTC"    day    = RESHAPE(qdy, T[gt=tax])
   define variable/title="hour"/unit="UTC"   hour   = RESHAPE(qhr, T[gt=tax])
   define variable/title="minute"/unit="UTC" minute = RESHAPE(qmi, T[gt=tax])
   define variable/title="second"/unit="UTC" second = RESHAPE(qse, T[gt=tax])
   !
   !cancel grid g1d
   !cancel axis xtdim
   !cancel variable nlines, tdim

cancel symbol l1, mstr, tzero
cancel variable mostr

say Output to ($s4dbase).nc 
save/quiet/clobber/file="($s4dbase).nc" \
    year, month, day, hour, minute, second, lon, lat, press
