// -*- kpp -*- kpp mode for emacs
// see example.rpl for details about replacement files

// Author: Patrick Joeckel : CCMI-base-01
//         Holger Tost     : OCS chemistry added
//         Christoph Bruehl: modified, see below
//         Patrick Joeckel : additional diagnostic tracers

// replace the comprehensive organic chemistry by mim1:
#include mim1

// modifications for the ESCiMo base run:
#include ESCiMo

// O3s diagnostic
#REPLACE <>
<G01Diag> O3s            = LO3s        : {%StTrG} k_O3s; {&1714}
#ENDREPLACE

//
// MODIFIED SULFUR CHEMISTRY FOR USAGE WITH INTERACTIVE AEROSOL
//
// this has been suggested by Ch. Bruehl, maybe with an original rate coefficient prior to JPL-2011
// #REPLACE <G9200>
// <a>  SO2    + OH    = SO3 + HO2 : {%TrStGS}   k_3rd(temp,cair,3.E-31,3.3,1.5E-12,0.,0.6); {&1555}
// #ENDREPLACE
//
// this is the original reaction in gas_mim1.eqn (JPL-2011):
// <G9200>  SO2 + OH {+O2+H2O} = H2SO4 + HO2  : {%StTrGS}   k_3rd(temp,cair,3.3E-31,4.3,1.6E-12,0.,0.6){§1.1}; {&2626}
// keeping the same reaction rate coefficient splitting the reaction yields:
#REPLACE <G9200>
<a>  SO2    + OH    = SO3 + HO2 : {%StTrG}   k_3rd(temp,cair,3.3E-31,4.3,1.6E-12,0.,0.6){§1.1}; {&2626}
#ENDREPLACE

//
// - G9202, J9002 and J9003 are new
// - <J9001>  SO2 + hv  = SO  + O3P   : {%TrStGSJ} JX(ip_SO2); {&SGN}
//   not used, because J_SO2 not correct, apply scaled J_OCS instead
//
#REPLACE <>
<G9100>  SO + O2   = SO2 + O3P   : {%TrStG} 1.25e-13*exp(-2190/temp); {&2626}
<G9101>  SO + O3   = SO2 + O2    : {%TrStG} 3.4e-12*exp(-1100/temp); {&2626}
<G9102>  S  + O2   = SO  + O3P   : {%TrStG} 2.3e-12; {&2626}
<G9201>  SH + O2   = OH  + SO    : {%TrStG} 4.e-19; {&2626}
<G9202>  SO3 + H2O = H2SO4       : {%TrStG} 8.5e-41*exp(6540./temp)*C(ind_H2O); {&1555}
<G9406>  OCS + OH  = SH  + CO2   : {%TrStG} 1.1e-13*exp(-1200./temp); {&2626}
<G9407>  OCS + O3P = CO  + SO    : {%TrStG} 2.1e-11*exp(-2200./temp); {&2626}
<J9000>  OCS + hv  = CO  + S     : {%TrStGJ} JX(ip_OCS); {&}
<J9001>  SO2 + hv  = SO  + O3P   : {%TrStGJ} 60.*JX(ip_OCS); {&}
<J9002>  SO3 + hv   = SO2 + O3P  : {%TrStGJ} JX(ip_SO3); {&}
<J9003>  H2SO4 + hv = SO3 + H2O  : {%TrStGJ} JX(ip_H2SO4); {&}
#ENDREPLACE

// additional diagnostic tracers (Note: these have NOT been present
// in REFC1-DLR-01, RC1-base-01, RC2-base-01
#REPLACE <>
<G10G6501>  CFCl3_c   + O1D  = O1D   : {%StG}   2.3E-10{§1.2}; {&2626} // equivalent reaction for diagnostic tracer
<J11J6500>  CFCl3_c   + hv   = Dummy : {%StGJ}  jx(ip_CFCl3){§}; {&2342} // equivalent reaction for diagnostic tracer
<G12G6500>  CF2Cl2_c  + O1D  = O1D   : {%StG}   1.4E-10{§1.25}; {&2626} // equivalent reaction for diagnostic tracer
<J13J6501>  CF2Cl2_c  + hv   = Dummy : {%StGJ}  jx(ip_CF2Cl2){§}; {&2342} // equivalent reaction for diagnostic tracer
<G14G3102a> N2O_c     + O1D  = O1D   : {%UpStG}   7.25E-11{§1.1}*EXP(20./temp); {&2626} // equivalent reaction for diagnostic tracer
<G15G3102b> N2O_c     + O1D  = O1D + Dummy : {%StG} 4.63E-11{§1.1}*EXP(20./temp); {&2626} // equivalent reaction for diagnostic tracer
<J16J3100>  N2O_c     + hv   = Dummy : {%UpStGJ}  jx(ip_N2O){§}; {&2342} // equivalent reaction for diagnostic tracer
<G17G6407>  CH3CCl3_c + O1D  = O1D   : {%StG}   3.E-10{§}; {&} // equivalent reaction for diagnostic tracer
<G18G6408>  CH3CCl3_c + OH   = OH    : {%StTrG} 1.64E-12{§1.15}*EXP(-1520./temp); {&2626} // equivalent reaction for diagnostic tracer
<J19J6402>  CH3CCl3_c + hv   = Dummy : {%StGJ}  jx(ip_CH3CCl3){§}; {&2342} // equivalent reaction for diagnostic tracer
<J20J7601>  CF2ClBr_c + hv   = Dummy : {%StGJ}  jx(ip_CF2ClBr){§}; {&2342} // equivalent reaction for diagnostic tracer
<J21J7500>  CF3Br_c   + hv   = Dummy : {%StGJ}  jx(ip_CF3Br){§}; {&2342} // equivalent reaction for diagnostic tracer
#ENDREPLACE
