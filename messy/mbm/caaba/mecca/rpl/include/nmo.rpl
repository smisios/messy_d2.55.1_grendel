// -*- kpp -*- kpp mode for emacs

// reduced NMO (new CH4 oxidation) chemistry mechanism
// authors: F.Frank (DLR-IPA), S. Gromov (MPI-C)

// should be applied after mim1.rpl + mim1-H2O_fix.rpl
// important: make sure to remove r-n <G4118> when isotope H tagging is used - this reaction is parameterised via 2-channel mechanism using MECCA-TAG

#REPLACE <G41*>
// 1C (#G41nn)
// op_ff_20160302+
// resolved CH3O:
<G4100>  CH4      + O1D      = .75 CH3 + .75 OH + .2 CH3O + .2 H + .05 HCHO + .05 H2 {+ 638. kJmol} : {%UpStG} 1.75E-10{§1.15}; {&2626}
// op_ff_20160302-
<G4101>  CH4      + OH       = CH3 + H2O          : {%StTrG} 1.85E-20{§1.2}*EXP(2.82*LOG(temp)-987./temp); {&1627}
<G4102>  CH3OH    + OH {+.85 O2} = .85 HCHO + .85 HO2 + .15 CH3O + H2O   : {%TrG} 6.38E-18{§§0.08}*(temp)**2*EXP(144./temp); {&1759}
<G4103a> CH3O2    + HO2      = CH3OOH + O2        : {%StTrG} 3.8E-13*EXP(780./temp)/(1.+1./498.*EXP(1160./temp)); {&1759}
<G4103b> CH3O2    + HO2      = HCHO + H2O + O2    : {%StTrG} 3.8E-13*EXP(780./temp)/(1.+498.*EXP(-1160./temp)); {&1759}
<G4104a> CH3O2    + NO       = CH3O + NO2         : {%StTrG} 2.3E-12{§§0.05}*EXP(360./temp)*(1.-beta_CH3NO3); {&1759, 3177, 3164}
<G4104b> CH3O2    + NO       = CH3NO3             : {%StTrG} 2.3E-12{§§0.05}*EXP(360./temp)*beta_CH3NO3; {&1759, 3177, 3164} // Methyl nitrate yield according to \citet{2130} but reduced by a factor of 10 according to the upper limit derived from measurements by \citet{2048}.
<G4105>  CH3O2    + NO3      = CH3O + NO2 + O2    : {%TrG} 1.2E-12{§§0.3}; {&1759}
<G4106a> CH3O2               = CH3O + .5 O2                : {%StTrG} 7.4E-13{§§0.15}*EXP(-520./temp)*RO2*2.; {&1759}
<G4106b> CH3O2               = .5 HCHO + .5 CH3OH + .5 O2  : {%StTrG} (k_CH3O2-7.4E-13{§§0.15}*EXP(-520./temp))*RO2*2.; {&1759}
<G4107>  CH3OOH   + OH       = .6 CH3O2 + .4 HCHO + .4 OH + H2O : {%StTrG} k_CH3OOH_OH; {&1745}
<G4108>  HCHO     + OH       = CO + H2O + HO2     : {%StTrG} 9.52E-18{§1.05}*EXP(2.03*LOG(temp)+636./temp); {&1634}
<G4109>  HCHO     + NO3      = HNO3 + CO + HO2    : {%TrG} 3.4E-13{§1.3}*EXP(-1900./temp); {&2626}
<G4110>  CO       + OH       = H + CO2 {+ 103. kJmol} : {%UpStTrG} (1.57E-13+cair*3.54E-33){§1.15}; {&1628}
<G4111>  HCOOH    + OH {+O2} = CO2 + HO2 + H2O    : {%TrG} 2.94E-14*exp(786./temp)+9.85E-13*EXP(-1036./temp){§}; {&3038}
<G4112>  CO       + O3P {+M} = CO2 {+ 516. kJmol} : {%UpStG} 6.60E-33*EXP(-1103./temp); {&2615}
<G4113>  CH4      + O3P {+O2}= .51 CH3 + .51 OH + .49 CH3O + .49 H {+ 638. kJmol} : {%UpStG} 6.03E-18{§}*(temp)**(2.17)*EXP(-3619./temp); {&2615, 3059, 3060}
<G4114>  CH3O2    + NO2      = CH3O2NO2           : {%StTrG} k_NO2_CH3O2; {&2626}
<G4115>  CH3O2NO2 {+ M}      = CH3O2 + NO2        : {%StTrG} k_NO2_CH3O2/(9.5E-29{§1.3}*EXP(11234./temp)); {&2626}
<G4116>  CH3O2NO2 + OH       = HCHO + NO3 + H2O   : {%StTrG} 3.00E-14{§} ; // Same value as for PAN + OH.
<G4117>  CH3NO3   + OH {+ O2}= H2O + HCHO + NO2   : {%StTrG} 4.0E-13{§}*EXP(-845./temp); {&1759}
<G4118>  CH3O     {+ O2}     = HO2 + HCHO         : {%StTrG} 1.3E-14{§}*exp(-663./temp)*c(ind_O2); {&3043}
<G4119a> CH3O     + NO2 {+ M}= CH3NO3             : {%StTrG} k_3rd_iupac(temp,cair,8.1E-29,4.5,2.1E-11,0.,0.44){§}; {&1759}
<G4119b> CH3O     + NO2      = HCHO + HONO        : {%StTrG} 9.6E-12*EXP(-1150./temp){§§0.5}; {&1759}
<G4120a> CH3O     + NO       = CH3ONO             : {%StTrG} k_3rd_iupac(temp,cair,2.6E-29,2.8,3.3E-11,0.6,REAL(EXP(-temp/900.),SP)); {&1759}
<G4120b> CH3O     + NO       = HCHO + HNO         : {%StTrG} 2.3E-12{§§0.2}*(temp/300.)**(0.7); {&1759}
<G4121>  CH3O2    + O3       = CH3O + 2 O2        : {%StTrG} 2.9E-16{§2.}*exp(-1000./temp); {&2626}
<G4122>  CH3ONO   + OH {+ O2}= H2O + HCHO + NO    : {%StTrG} 1.E-10{§}*exp(-1764./temp); {&3044}
<G4133>  CH3O2    + OH       = CH3O + HO2         : {%StTrG} 1.4E-10{§}; {&3042} // Lower limit of the rate constant. Products uncertain but \chem{CH_3OH} can be excluded because of a likely high energy barrier (L.\ Vereecken, pers. comm.). \chem{CH_2OO} production cannot be excluded.
<G4151>  CH3      + O2 {+ M} = CH3O2              : {%StTrG} k_3rd_iupac(temp,cair,7.0E-31,3.,1.8E-12,-1.1,0.33){§}; {&1759}
<G4152>  CH3      + O3       = .956 HCHO + .956 H + .044 CH3O + O2 : {%StTrG} 5.1E-12{§}*exp(-210./temp); {&3056, 3057}
<G4153>  CH3      + O3P      = .83 HCHO + .83 H + .17 CO + .17 H2 + .17 H : {%StTrG} 1.3E-10{§}; {&1759}
<G4154>  CH3O     + O3       = CH3O2 + O2         : {%StTrG} 2.53E-14{§}; {&3056} // Products assumed to be \chem{CH_3O_2 + O_2} (could also be \chem{HCHO + O_2 + OH}).
<G4155>  CH3O     + O3P      = .75 CH3 + .75 O2 + .25 HCHO + .25 OH  : {%StTrG} 2.5E-11{§}; {&3037}
<G4156>  CH3O2    + O3P      = CH3O + O2          : {%StTrG} 4.3E-11{§1.46}; {&3061}
<G4157>  HCHO     + O3P      = .7 OH + .7 CO + .3 H + .3 CO2 + HO2 : {%StTrG} 3.E-11*EXP(-1600./temp){§1.3}; {&2626}
#ENDREPLACE

// mz_sg_20160302+ ----- forgotten parts of NMO --------------------------------

#REPLACE <J41*>
// 1C (#J41nnn)
<J41000>  CH3OOH      + hv = CH3O + OH                  : {%StTrGJ} jx(ip_CH3OOH){§}; {&2342}
<J41001a> HCHO        + hv = H2 + CO                    : {%StTrGJ} jx(ip_COH2){§}; {&2342}
<J41001b> HCHO        + hv = H + CO + HO2               : {%StTrGJ} jx(ip_CHOH){§}; {&2342}
<J41002>  CO2         + hv = CO + O3P                   : {%StGJ}   jx(ip_CO2){§}; {&2342}
// <J41003>  CH4         + hv = .42 CH3 + .42 H + .6912 H2 + .0864 HCHO + .0864 O3P + .1584 OH + .1584 HO2 + .2112 CO2 + .1824 CO + .024 H2O + .10 LCARBON : {\%StGJ!{\ jx(ip_CH4){\§!{\; {\&2342!{\ // \chem{\CH_3!{\- and \chem{\CH_2!{\-channels are considered only and with their branching ratios being 0.42 and 0.48, respectively \citep{\3058!{\. \chem{\CH!{\-production is neglected. \chem{\CH_2!{\ is assumed to react only with O2 yielding 1.44 \chem{\H_2!{\ + .18 \chem{\HCHO!{\ + .18 \chem{\O(^3P)!{\ + .33 \chem{\OH!{\ + .33 \chem{\HO_2!{\ + .44 \chem{\CO_2!{\ + .38 \chem{\CO!{\ + .05 \chem{\H_2O!{\ as assumed in the WACCM model by J. Orlando (Doug Kinnison, pers. comm. with D. Taraborrelli). }
<J41003>  CH4         + hv = .42 CH3 + .62 H + .7912 H2 + .0864 HCHO + .0864 O3P + .1584 OH + .1584 HO2 + .2112 CO2 + .1824 CO + .024 H2O + .10 LCARBON : {%StGJ} jx(ip_CH4){§}; {&2342} // \chem{CH_3}- and \chem{CH_2}-channels are considered only and with their branching ratios being 0.42 and 0.48, respectively \citep{3058}. \chem{CH}-production goes into LCARBON, 0.2 H and 0.1 H2 is added for this branch to balance hydrogen. \chem{CH_2} is assumed to react only with O2 yielding 1.44 \chem{H_2} + .18 \chem{HCHO} + .18 \chem{O(^3P)} + .33 \chem{OH} + .33 \chem{HO_2} + .44 \chem{CO_2} + .38 \chem{CO} + .05 \chem{H_2O} as assumed in the WACCM model by J. Orlando (Doug Kinnison, pers. comm. with D. Taraborrelli).
<J41004>  CH3ONO      + hv = CH3O + NO                  : {%StTrGJ} jx(ip_CH3ONO); {&2342}
<J41005>  CH3NO3      + hv = CH3O + NO2                 : {%StTrGJ} jx(ip_CH3NO3); {&2342}
<J41006>  CH3O2NO2    + hv = .667 NO2 + .667 CH3O2 + .333 NO3 + .333 CH3O : {%StTrGJ} jx(ip_CH3O2NO2); {&2342} // product distribution as for HNO4
<J41008>  CH3O2       + hv = HCHO + OH                  : {%StTrGJ} jx(ip_CH3O2); {&2342}
<J41009>  HCOOH       + hv = CO + HO2 + OH              : {%StTrGJ} jx(ip_HCOOH); {&2342}
#ENDREPLACE

// remaining C1-related chemistry

// changes are mostly:
// - updating Cl+H/C chemistry and additional H pathways
// - resolving CH3 (instead of direct CH3O2 production in MIM1)

#REPLACE <G6400>
<>  Cl      + CH4    = HCl + CH3         : {%StTrG} 6.6E-12{§§0.06}*EXP(-1240./temp); {&1759}
#ENDREPLACE 

#REPLACE <>  
<G6413>  Cl      + CH3NO3 = HCl + HCHO + NO2   : {%StTrG} 1.3E-11{§1.5}*EXP(-1200./temp); {&2626}
#ENDREPLACE

#REPLACE <>  
<G6414> Cl      + CH3ONO = HCl + HCHO + NO    : {%StTrG} 2.1E-12{§}; {&3063}
#ENDREPLACE

#REPLACE <>  
// CH2OO = CO + HO2 + OH
<G6415>  Cl      + CH3O2  = .5 ClO + .5 CH3O + .5 HCl + .5 CO + .5 HO2 + .5 OH : {%StTrG} 1.6E-10{§1.5}; {&2626}
#ENDREPLACE

// CH3O2 -> CH3
#REPLACE <G9402>
<>  DMSO   + OH    = .6 H2O + .6 SO2 + HCHO + .6 CH3 +   .4 HO2 + .4 CH3SO3H : {%TrG} 1.E-10{§1.3}{1.0+/-0.3}; {&746}
#ENDREPLACE
#REPLACE <G9403>
<>  CH3SO2         = SO2 + CH3        : {%TrG} 1.8E13{§}*EXP(-8661./temp); {&749}
#ENDREPLACE

// photolytic reactions
// CH3O2 -> CH3
#REPLACE <J6400>
<> CH3Cl  + hv = Cl + CH3             : {%StGJ}    jx(ip_CH3Cl){§}; {&2342}
#ENDREPLACE
#REPLACE <J7400>
<> CH3Br  + hv = Br + CH3             : {%StGJ} jx(ip_CH3Br){§}; {&2342}
#ENDREPLACE
#REPLACE <J8401a>
<> CH3I    + hv = CH3                 : {%StTrGJ}    JX(ip_CH3I); {&2342}
#ENDREPLACE
// mz_sg_20160302- -------------------------------------------------------------
