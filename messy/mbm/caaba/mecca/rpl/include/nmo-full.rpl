// -*- kpp -*- kpp mode for emacs

// full NMO (new CH4 oxidation) chemistry mechanism
// authors: S. Gromov (MPI-C), F.Frank (DLR-IPA)

// should be applied after mim1.rpl
// important: make sure to remove r-n <G4118> when isotope H tagging is used - this reaction is parameterised via 2-channel mechanism using MECCA-TAG


// mz_sg_20160215+ ----- fixing H2O production missing in MIM1 -----------------

#REPLACE <G9400a>
<> DMS + OH = CH3SO2 + HCHO + H2O : {%TrG} 1.13E-11*EXP(-253./temp); {&1610} // For the abstraction path, the assumed reaction sequence (omitting \chem{H_2O} and \chem{O_2} as products) according to \citet{243} is: \begin{eqnarray*} \chem{DMS} + \chem{OH} & \TO & \chem{CH_3SCH_2}\\ \chem{CH_3SCH_2} + \chem{O_2} & \TO & \chem{CH_3SCH_2OO}\\ \chem{CH_3SCH_2OO} + \chem{NO} & \TO & \chem{CH_3SCH_2O} + \chem{NO_2}\\ \chem{CH_3SCH_2O} & \TO & \chem{CH_3S} + \chem{HCHO}\\ \chem{CH_3S} + \chem{O_3} & \TO & \chem{CH_3SO}\\ \chem{CH_3SO} + \chem{O_3} & \TO & \chem{CH_3SO_2}\\ \hline \chem{DMS}+\chem{OH}+\chem{NO}+2\chem{O_3} & \TO & \chem{CH_3SO_2}+\chem{HCHO}+\chem{NO_2} \end{eqnarray*} Neglecting the effect on \chem{O_3} and \chem{NO_x}, the remaining reaction is: $$\chem{DMS} + \chem{OH} + \chem{O_3} \TO \chem{CH_3SO_2} + \chem{HCHO}$$.
#ENDREPLACE

// it is OK that this one is replaced once more further
#REPLACE <G9402>
<> DMSO + OH = .6 H2O + .6 SO2 + HCHO + .6 CH3O2 + .4 HO2 + .4 CH3SO3H : {%TrG} 1.E-10{1.0+/-0.3}; {&746}
#ENDREPLACE

// mz_sg_20160215- -------------------------------------------------------------



// mz_sg_20160220+ ----- NMO mechanism -----------------------------------------
// new methane chemistry (resolves H exchange better) - full version (SG)

#REPLACE <G41*>
// 1C (#G41nn)
// op_ff_20160302+
// current gas.eqn:
//<G4100>  CH4      + O1D      = .75 CH3 + .75 OH + .25 HCHO + .4 H + .05 H2 {+ 638. kJmol} : {%UpStG} 1.75E-10{§1.15}; {&2626}
// correction for H transfer:
//<G4100>  CH4      + O1D {+.2 O2} = .75 CH3 + .75 OH + .25 HCHO + .2 H + .2 HO2 + .05 H2 {+ 638. kJmol} : {%UpStG} 1.75E-10{§1.15}; {&2626}
// resolved CH3O:
<G4100>  CH4      + O1D      = .75 CH3 + .75 OH + .2 CH3O + .2 H + .05 HCHO + .05 H2 {+ 638. kJmol} : {%UpStG} 1.75E-10{§1.15}; {&2626}
// op_ff_20160302-
<G4101>  CH4      + OH       = CH3 + H2O          : {%StTrG} 1.85E-20{§1.2}*EXP(2.82*LOG(temp)-987./temp); {&1627}
<G4102>  CH3OH    + OH {+.85 O2} = .85 HCHO + .85 HO2 + .15 CH3O + H2O   : {%TrG} 6.38E-18{§§0.08}*((temp)**2)*EXP(144./temp); {&1759}
<G4103a> CH3O2    + HO2      = CH3OOH + O2        : {%StTrG} 3.8E-13*EXP(780./temp)/(1.+1./498.*EXP(1160./temp)); {&1759}
<G4103b> CH3O2    + HO2      = HCHO + H2O + O2    : {%StTrG} 3.8E-13*EXP(780./temp)/(1.+498.*EXP(-1160./temp)); {&1759}
<G4104a> CH3O2    + NO       = CH3O + NO2         : {%StTrG} 2.3E-12{§§0.05}*EXP(360./temp)*(1.-beta_CH3NO3); {&1759, 3177, 3164}
<G4104b> CH3O2    + NO       = CH3NO3             : {%StTrG} 2.3E-12{§§0.05}*EXP(360./temp)*beta_CH3NO3; {&1759, 3177, 3164} // Methyl nitrate yield according to \citet{2130} but reduced by a factor of 10 according to the upper limit derived from measurements by \citet{2048}.
<G4105>  CH3O2    + NO3      = CH3O + NO2 + O2    : {%TrG} 1.2E-12{§§0.3}; {&1759}
<G4106a> CH3O2               = CH3O + .5 O2                : {%StTrG} 7.4E-13{§§0.15}*EXP(-520./temp)*RO2*2.; {&1759}
<G4106b> CH3O2               = .5 HCHO + .5 CH3OH + .5 O2  : {%StTrG} (k_CH3O2-7.4E-13{§§0.15}*EXP(-520./temp))*RO2*2.; {&1759}
<G4107>  CH3OOH   + OH       = .6 CH3O2 + .4 HCHO + .4 OH + H2O : {%StTrG} k_CH3OOH_OH; {&1745}
<G4108>  HCHO     + OH       = CO + H2O + HO2     : {%StTrG} 9.52E-18{§1.05}*EXP(2.03*LOG(temp)+636./temp); {&1634}
<G4109>  HCHO     + NO3      = HNO3 + CO + HO2    : {%TrG} 3.4E-13{§1.3}*EXP(-1900./temp); {&2626}
<G4110>  CO       + OH       = H + CO2 {+ 103. kJmol} : {%UpStTrG} (1.57E-13+cair*3.54E-33){§1.15}; {&1628}
<G4111>  HCOOH    + OH {+O2} = CO2 + HO2 + H2O    : {%TrG} 2.94E-14*exp(786./temp)+9.85E-13*EXP(-1036./temp){§}; {&3038}
<G4112>  CO       + O3P {+M} = CO2 {+ 516. kJmol} : {%UpStG} 6.60E-33*EXP(-1103./temp); {&2615}
<G4113>  CH4      + O3P {+O2}= .51 CH3 + .51 OH + .49 CH3O + .49 H {+ 638. kJmol} : {%UpStG} 6.03E-18{§}*(temp)**(2.17)*EXP(-3619./temp); {&2615, 3059, 3060}
<G4114>  CH3O2    + NO2      = CH3O2NO2           : {%StTrG} k_NO2_CH3O2; {&2626}
<G4115>  CH3O2NO2 {+ M}      = CH3O2 + NO2        : {%StTrG} k_NO2_CH3O2/(9.5E-29{§1.3}*EXP(11234./temp)); {&2626}
<G4116>  CH3O2NO2 + OH       = HCHO + NO3 + H2O   : {%StTrG} 3.00E-14{§} ; // Same value as for PAN + OH.
<G4117>  CH3NO3   + OH {+ O2}= H2O + HCHO + NO2   : {%StTrG} 4.0E-13{§}*EXP(-845./temp); {&1759}
<G4118>  CH3O     {+ O2}     = HO2 + HCHO         : {%StTrG} 1.3E-14{§}*exp(-663./temp)*c(ind_O2); {&3043}
<G4119a> CH3O     + NO2 {+ M}= CH3NO3             : {%StTrG} k_3rd_iupac(temp,cair,8.1E-29,4.5,2.1E-11,0.,0.44){§}; {&1759}
<G4119b> CH3O     + NO2      = HCHO + HONO        : {%StTrG} 9.6E-12*EXP(-1150./temp){§§0.5}; {&1759}
<G4120a> CH3O     + NO       = CH3ONO             : {%StTrG} k_3rd_iupac(temp,cair,2.6E-29,2.8,3.3E-11,0.6,REAL(EXP(-temp/900.),SP)); {&1759}
<G4120b> CH3O     + NO       = HCHO + HNO         : {%StTrG} 2.3E-12{§§0.2}*(temp/300.)**(0.7); {&1759}
<G4121>  CH3O2    + O3       = CH3O + 2 O2        : {%StTrG} 2.9E-16{§2.}*exp(-1000./temp); {&2626}
<G4122>  CH3ONO   + OH {+ O2}= H2O + HCHO + NO    : {%StTrG} 1.E-10{§}*exp(-1764./temp); {&3044}
<G4123>  HCHO     + HO2      = HOCH2O2            : {%StTrG} 9.7E-15{§§0.3}*EXP(625./temp); {&1759}
<G4124>  HOCH2O2             = HCHO + HO2         : {%StTrG} 2.4E12{§§0.3}*EXP(-7000./temp); {&1759}
<G4125>  HOCH2O2  + HO2      = .5 HOCH2OOH + .5 HCOOH + .2 OH + .2 HO2 + .3 H2O + .8 O2 : {%StTrG} 5.6E-15{§§0.3}*EXP(2300./temp); {&1759}
<G4126>  HOCH2O2  + NO {+ O2}= NO2 + HO2 + HCOOH  : {%StTrG} 0.7275*2.3E-12*EXP(360./temp); {&1759} // Same as for G4104 but scaled to match the recommeded value at 298K.
<G4127>  HOCH2O2  + NO3      = NO2 + HO2 + HCOOH  : {%StTrG} 1.2E-12{§§0.3};  // Same as for CH3O2 + NO3 in G4105.
<G4129a> HOCH2O2             = HCOOH + HO2                   : {%StTrG} (k_CH3O2*5.5E-12{§§0.3})**(0.5)*RO2*2.; {&1759}
<G4129b> HOCH2O2             = .5 HCOOH + .5 HOCH2OH + .5 O2 : {%StTrG} (k_CH3O2*5.7E-14{§§0.3}*EXP(750./temp))**(0.5)*RO2*2.; {&1759}
<G4130a> HOCH2OOH + OH       = HOCH2O2 + H2O      : {%StTrG} 0.6*k_CH3OOH_OH; {&3030} // SAR for H-abstraction by OH.
<G4130b> HOCH2OOH + OH       = HCOOH + H2O + OH   : {%StTrG} k_ROHRO + k_s*f_sOOH*f_sOH; {&3030} // SAR for H-abstraction by OH.
<G4132>  HOCH2OH  + OH {+ O2}= HO2 + HCOOH + H2O  : {%StTrG} k_ROHRO + 2.*k_s*f_sOH*f_sOH; {&3030} // SAR for H-abstraction by OH.
<G4133>  CH3O2    + OH       = CH3O + HO2         : {%StTrG} 1.4E-10{§}; {&3042} // Lower limit of the rate constant. Products uncertain but \chem{CH_3OH} can be excluded because of a likely high energy barrier (L.\ Vereecken, pers. comm.). \chem{CH_2OO} production cannot be excluded.
<G4134>  CH2OO    {+ O2}     = CO + HO2 + OH      : {%StTrG} 1.124E+14{§}*EXP(-10000./temp); // Estimate based on the decomposition lifetime of 3 s \citep{3047} and a 20 kcal/mol energy barrier \citep{3040}.
<G4135>  CH2OO    + H2O      = HOCH2OOH           : {%StTrG} k_CH2OO_NO2*3.6E-6; {&3048} // Rate constant for \chem{CH_2OO + NO_2} (G4138) multiplied by the factor from \citet{3048}.
<G4136>  CH2OO    + H2OH2O   = HOCH2OOH + H2O     : {%StTrG} 5.2E-12; {&3173, 3174} // Average of two measurements.
<G4137>  CH2OO    + NO       = HCHO + NO2         : {%StTrG} 6.E-14{§}; {&3041} // Upper limit.
<G4138>  CH2OO    + NO2      = HCHO + NO3         : {%StTrG} k_CH2OO_NO2; {&3041, 3049} // Average of 7.E-12 and 1.5E-12.
//<G4139> new reaction number G9408
<G4140>  CH2OO    + CO       = HCHO + CO2         : {%StTrG} 3.6E-14{§}; {&3039}
<G4141>  CH2OO    + HCOOH    = 2 HCOOH            : {%StTrG} 1.E-10{§}; {&3051} // \chem{HOOCH_2OCHO} forms and then decomposes to formic anhydride \citep{2071} which hydrolyses in the humid atmosphere \citep{3045}.
<G4142>  CH2OO    + HCHO     = 2 LCARBON          : {%StTrG} 1.7E-12{§}; {&3049} // High-pressure limit.
<G4143>  CH2OO    + CH3OH    = 2 LCARBON          : {%StTrG} 5.E-12{§}; {&3039} // Generic estimate for reaction with alcohols.
<G4144>  CH2OO    + CH3O2    = 2 LCARBON          : {%StTrG} 5.E-12{§}; {&3039} // Generic estimate for reaction with \chem{RO_2}.
<G4145>  CH2OO    + HO2      = LCARBON            : {%StTrG} 5.E-12{§}; {&3039}
<G4146>  CH2OO    + O3       = HCHO + 2 O2        : {%StTrG} 1.E-12{§}; {&3053}
<G4147>  CH2OO    + CH2OO    = 2 HCHO + O2        : {%StTrG} 6.E-11{§}; {&3054}
<G4148>  HOCH2O2  + NO2      = HOCH2O2NO2         : {%StTrG} k_NO2_CH3O2; {&2626}
<G4149>  HOCH2O2NO2 {+ M}    = HOCH2O2 + NO2      : {%StTrG} k_NO2_CH3O2/(9.5E-29{§1.3}*EXP(11234./temp)); {&2626, 3055} // \citet{3055} estimated a decomposition rate equal to that of \chem{CH_3O_2NO_2}.
<G4150>  HOCH2O2NO2 + OH     = HCOOH + NO3 + H2O  : {%StTrG} 9.50E-13{§}*EXP(-650./temp)*f_sOH; // Value for \chem{CH_3O_2NO_2} + OH, H-abstraction enhanced by the HO-group by f\_sOH.
<G4151>  CH3      + O2 {+ M} = CH3O2              : {%StTrG} k_3rd_iupac(temp,cair,7.0E-31,3.,1.8E-12,-1.1,0.33){§}; {&1759}
<G4152>  CH3      + O3       = .956 HCHO + .956 H + .044 CH3O + O2 : {%StTrG} 5.1E-12{§}*exp(-210./temp); {&3056, 3057}
<G4153>  CH3      + O3P      = .83 HCHO + .83 H + .17 CO + .17 H2 + .17 H : {%StTrG} 1.3E-10{§}; {&1759}
<G4154>  CH3O     + O3       = CH3O2 + O2         : {%StTrG} 2.53E-14{§}; {&3056} // Products assumed to be \chem{CH_3O_2 + O_2} (could also be \chem{HCHO + O_2 + OH}).
<G4155>  CH3O     + O3P      = .75 CH3 + .75 O2 + .25 HCHO + .25 OH  : {%StTrG} 2.5E-11{§}; {&3037}
<G4156>  CH3O2    + O3P      = CH3O + O2          : {%StTrG} 4.3E-11{§1.46}; {&3061}
<G4157>  HCHO     + O3P      = .7 OH + .7 CO + .3 H + .3 CO2 + HO2 : {%StTrG} 3.E-11*EXP(-1600./temp){§1.3}; {&2626}
//*<G4158>  CH2OOA              = .37 CH2OO + .47 CO + .47 H2O + .16 HO2 + .16 CO + .16 OH : {%TrG} KDEC; {&1759}
<G4159>  HCN      + OH       = H2O + CN           : {%TrG} k_3rd(temp,cair,4.28E-33,1.0,REAL(4.25E-13*EXP(-1150./temp),SP),1.0,0.8); {&3191}
<G4160a> HCN      + O1D      = O3P + HCN          : {%TrG} 1.08E-10*EXP(105./temp)*0.15*EXP(200./temp); {&3197}
<G4160b> HCN      + O1D      = H + NCO            : {%TrG} 1.08E-10*EXP(105./temp)*0.68/2.; {&3197} // Half of the H-yield is attributed to fast secondary chemistry.
<G4160c> HCN      + O1D      = OH + CN            : {%TrG} 1.08E-10*EXP(105./temp)*(1.-(0.68/2.+0.15*EXP(200./temp))); {&3197} // The NH + CO channel is also significant but neglected here.
<G4161>  HCN      + O3P      = H + NCO            : {%TrG} 1.0E-11*EXP(-4000./temp); {&2626} // No studies below 450 K and only the major channel is considered.
<G4162>  CN       + O2       = NCO + O3P          : {%TrG} 1.2E-11*EXP(210./temp)*0.75; {&3037}
<G4163>  CN       + O2       = CO +  NO           : {%TrG} 1.2E-11*EXP(210./temp)*0.25; {&3037}
<G4164>  NCO      + O2       = CO2 + NO           : {%TrG} 7.E-15; {&3198} // Upper limit. Dominant pathway under atmospheric conditions.
#ENDREPLACE

// mz_sg_20160220- -------------------------------------------------------------



// mz_sg_20160302+ ----- forgotten parts of NMO --------------------------------

#REPLACE <J41*>
// 1C (#J41nnn)
<J41000>  CH3OOH      + hv = CH3O + OH                  : {%StTrGJ} jx(ip_CH3OOH){§}; {&2342}
<J41001a> HCHO        + hv = H2 + CO                    : {%StTrGJ} jx(ip_COH2){§}; {&2342}
<J41001b> HCHO        + hv = H + CO + HO2               : {%StTrGJ} jx(ip_CHOH){§}; {&2342}
<J41002>  CO2         + hv = CO + O3P                   : {%StGJ}   jx(ip_CO2){§}; {&2342}
<J41003>  CH4         + hv = .42 CH3 + .42 H + .6912 H2 + .0864 HCHO + .0864 O3P + .1584 OH + .1584 HO2 + .2112 CO2 + .1824 CO + .024 H2O + .10 LCARBON : {%StGJ} jx(ip_CH4){§}; {&2342} // \chem{CH_3}- and \chem{CH_2}-channels are considered only and with their branching ratios being 0.42 and 0.48, respectively \citep{3058}. \chem{CH}-production is neglected. \chem{CH_2} is assumed to react only with O2 yielding 1.44 \chem{H_2} + .18 \chem{HCHO} + .18 \chem{O(^3P)} + .33 \chem{OH} + .33 \chem{HO_2} + .44 \chem{CO_2} + .38 \chem{CO} + .05 \chem{H_2O} as assumed in the WACCM model by J. Orlando (Doug Kinnison, pers. comm. with D. Taraborrelli).
<J41004>  CH3ONO      + hv = CH3O + NO                  : {%StTrGJ} jx(ip_CH3ONO); {&2342}
<J41005>  CH3NO3      + hv = CH3O + NO2                 : {%StTrGJ} jx(ip_CH3NO3); {&2342}
<J41006>  CH3O2NO2    + hv = .667 NO2 + .667 CH3O2 + .333 NO3 + .333 CH3O : {%StTrGJ} jx(ip_CH3O2NO2); {&2342} // product distribution as for HNO4
<J41007>  HOCH2OOH    + hv = HCOOH + OH + HO2           : {%StTrGJ} jx(ip_CH3OOH); {&2342}
<J41008>  CH3O2       + hv = HCHO + OH                  : {%StTrGJ} jx(ip_CH3O2); {&2342}
<J41009>  HCOOH       + hv = CO + HO2 + OH              : {%StTrGJ} jx(ip_HCOOH); {&2342}
<J41010>  HOCH2O2NO2  + hv = .667 NO2 + .667 HOCH2O2 + .333 NO3 + .333 HCOOH + .333 HO2 : {%StTrGJ} jx(ip_CH3O2NO2); {&2342}
#ENDREPLACE

// remaining C1-related chemistry

// changes are mostly:
// - updating Cl+H/C chemistry and additional H pathways
// - resolving CH3 (instead of direct CH3O2 production in MIM1)

#REPLACE <G62*>
// Cl + H (#G62nn)
<G6200>  Cl + H2         = HCl + H     : {%StG}     3.9E-11{§§0.1}*EXP(-2310./temp); {&1845}
<G6201a> Cl + HO2        = HCl + O2    : {%StG}     4.4E-11-7.5E-11{§§0.2}*EXP(-620./temp); {&1845}
<G6201b> Cl + HO2        = ClO + OH    : {%StG}     7.5E-11{§§0.2}*EXP(-620./temp); {&1845}
<G6202>  Cl + H2O2       = HCl + HO2   : {%StTrG}   1.1E-11{§§0.2}*EXP(-980./temp); {&1845}
<G6203>  ClO + OH        = .94 Cl + .94 HO2 + .06 HCl + .06 O2 : {%StG} 7.3E-12{§§0.15}*EXP(300./temp); {&1845}
<G6204>  ClO + HO2       = HOCl + O2   : {%StTrG}   2.2E-12{§§0.2}*EXP(340./temp); {&1845} // At low temperatures, there may be a minor reaction channel leading to \kpp{O3}+\kpp{HCl}. See \citet{1626} for details. It is neglected here.
<G6205>  HCl + OH        = Cl  + H2O   : {%StTrG}   1.7E-12{§§0.06}*EXP(-230./temp); {&1845}
<G6206>  HOCl + OH       = ClO + H2O   : {%StG}     3.0E-12{§3.0}*EXP(-500./temp); {&2626}
#ENDREPLACE

#REPLACE <G64*>
<G6400>  Cl      + CH4    = HCl + CH3         : {%StTrG} 6.6E-12{§§0.06}*EXP(-1240./temp); {&1759}
<G6401>  Cl      + HCHO   = HCl + CO + HO2    : {%StTrG} 8.1E-11{§§0.06}*EXP(-34./temp); {&1759}
<G6402>  Cl      + CH3OOH = HCHO + HCl + OH   : {%StTrG} 5.9E-11{§§0.5}; {&1759} // The initial products are probably \chem{HCl} and \chem{CH_2OOH} \citep{1759}. It is assumed that \chem{CH_2OOH} dissociates into \chem{HCHO} and \chem{OH}.
<G6403>  ClO     + CH3O2  = HO2 + Cl + HCHO   : {%StTrG} 3.3E-12{§1.5}*EXP(-115./temp); {&2626}
<G6404>  CCl4    + O1D    = LCARBON + ClO + 3 Cl {+...} : {%StG} 3.3E-10{§1.2}; {&2626}
<G6405>  CH3Cl   + O1D    = LCARBON + OH + Cl {+...} : {%StG} 1.65E-10{§}; // Average of reactions with \chem{CH_3Br} and \chem{CH_3F} from \citet{1945} (B.\ Steil, pers.\ comm.).
<G6406>  CH3Cl   + OH     = LCARBON + H2O + Cl {+...} : {%StG} 2.4E-12{§1.15}*EXP(-1250./temp); {&2626}
<G6407>  CH3CCl3 + O1D    = 2 LCARBON + OH + 3 Cl {+...} : {%StG} 3.E-10{§}; // Rough extrapolation from reactions with \chem{CH_3CF_3}, \chem{CH_3CClF_2}, and \chem{CH_3CCl_2F} from \citet{1945}.
<G6408>  CH3CCl3 + OH     = 2 LCARBON + H2O + 3 Cl {+...} : {%StTrG} 1.64E-12{§1.15}*EXP(-1520./temp); {&2626}
<G6409>  Cl + C2H4 {+O2}  = HOCH2CH2O2 + HCl  : {%TrG} k_3rd_iupac(temp,cair,1.85E-29{§§0.5},3.3,6.0E-10{§§0.3},0.0,0.4); {&1759} // It is assumed that the reaction liberates all \kpp{Cl} atoms in the form of \kpp{HCl}.
<G6410>  Cl      + CH3CHO = HCl + CH3CO      : {%TrG} 8.0e-11{§§0.07}; {&1759}
<G6411>  C2H2    + Cl     = LCARBON + CH3 + HCl       : {%TrG} k_3rd_iupac(temp,cair,6.1e-30{§§0.3},3.0,2.0e-10{§§0.3},0.,0.6); {&1759}
<G6412>  C2H6    + Cl     = C2H5O2 + HCl       : {%TrG} 8.3E-11{§§0.06}*EXP(-100./temp); {&1759}
<G6413>  Cl      + CH3NO3 = HCl + HCHO + NO2   : {%StTrG} 1.3E-11{§1.5}*EXP(-1200./temp); {&2626}
<G6414>  Cl      + CH3ONO = HCl + HCHO + NO    : {%StTrG} 2.1E-12{§}; {&3063}
<G6415>  Cl      + CH3O2  = .5 ClO + .5 CH3O + .5 HCl + .5 CH2OO : {%StTrG} 1.6E-10{§1.5}; {&2626}
<G6416>  Cl      + CH3CN  = NCCH2O2 + HCl      : {%TrG} 1.6E-11{§}*EXP(-2104./temp); {&3211, 3212, 3031}
#ENDREPLACE

// may be an essential update w.r.t. HOx
// former study (doi:10.1029/97GL02686) speculates on r-n channels
// newer study (doi:10.1021/jp5108203) suggests CH2OO being the main product
#REPLACE <G7402*>
<G7402>  BrO  + CH3O2    = HOBr + CH2OO     : {%TrG} 2.42E-14{§}*EXP(1617./temp); {&3205}
#ENDREPLACE

// CH3O2 -> CH3
#REPLACE <G9402>
<>  DMSO   + OH    = .6 H2O + .6 SO2 + HCHO + .6 CH3 +   .4 HO2 + .4 CH3SO3H : {%TrG} 1.E-10{§1.3}{1.0+/-0.3}; {&746}
#ENDREPLACE
#REPLACE <G9403>
<>  CH3SO2         = SO2 + CH3        : {%TrG} 1.8E13{§}*EXP(-8661./temp); {&749}
#ENDREPLACE

// new reaction
#REPLACE <>
<G9408>  CH2OO + SO2 {+H2O} = H2SO4 + HCHO : {%StTrG} k_CH2OO_SO2; {&3041, 3049} // Average of 3.9E-11 and 3.42E-11.
#ENDREPLACE

// photolytic reactions
// CH3O2 -> CH3
#REPLACE <J6400>
<> CH3Cl  + hv = Cl + CH3             : {%StGJ}    jx(ip_CH3Cl){§}; {&2342}
#ENDREPLACE
#REPLACE <J7400>
<> CH3Br  + hv = Br + CH3             : {%StGJ} jx(ip_CH3Br){§}; {&2342}
#ENDREPLACE
// non-ESCiMO version
#REPLACE <J8401>
<> CH3I    + hv = I + CH3           : {%TrGJ}  jx(ip_CH3I){§}; {&2342}
#ENDREPLACE
// ESCiMO version
//#REPLACE <J8401a>
//<> CH3I    + hv = CH3                 : {%StTrGJ}    JX(ip_CH3I); {&2342}
//#ENDREPLACE
#REPLACE <J8402>
<> C3H7I   + hv = 2 LCARBON + I + CH3 : {%TrGJ}  jx(ip_C3H7I){§}; {&2342}
#ENDREPLACE

// mz_sg_20160302- -------------------------------------------------------------
